<?xml version="1.0"?>
<!-- @mxmlc -library-path+=./classes -source-path+=./classes -->
<mx:Application
	pageTitle="AMF3 Echo Client"
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:fx="com.fusiox.ui.*"
	initialize="initApp()"
	backgroundColor="#F4F4F4" themeColor="haloBlue" layout="vertical">

    <mx:Script>
    <![CDATA[
		import mx.controls.Alert;
		import org.red5.samples.echo.EchoClass;
		import org.red5.samples.echo.RemoteClass;
		import org.red5.samples.echo.ExternalizableClass;
		
		[Bindable]
		private var nc: NetConnection;
		
		[Bindable]
		private var testParams: Array;
		
		[Bindable]
		private var testIndex: Number;
		
		[Bindable]
		private var AMF0Count: Number;
		
		[Bindable]
		private var testsFailed: Number;
		
        private function initApp(): void
        {
			// Prepare test values
			testParams = new Array();
			testParams.push(null);
			testParams.push(true);
			testParams.push(false);
			testParams.push("");
			testParams.push("Hello world!");
			var i: Number;
			var longString: String = "";
			for (i=0; i<4000; i++)
				longString = longString + "0123456789";
			testParams.push(longString);
			var reallyLongString: String = "";
			for (i=0; i<7000; i++)
				reallyLongString = reallyLongString + "0123456789";
			testParams.push(reallyLongString);
			var evenLongerString: String = "";
			for (i=0; i<100000; i++)
				evenLongerString = evenLongerString + "0123456789";
			testParams.push(evenLongerString);
			var strings: Array = new Array();
			strings.push("test");
			strings.push("test");
			strings.push("test");
			strings.push("test");
			testParams.push(strings);
			testParams.push(0);
			testParams.push(1);
			testParams.push(-1);
			testParams.push(256);
			testParams.push(-256);
			testParams.push(65536);
			testParams.push(-65536);
			testParams.push(0.0);
			testParams.push(1.5);
			testParams.push(-1.5);
			testParams.push(new Array());
			var tmp1: Array = new Array();
			tmp1.push(1);
			testParams.push(tmp1);
			testParams.push(new Array(1, 2));
			testParams.push(new Array(1, 2, 3));
			var tmp2: Array = new Array();
			tmp2.push(1);
			tmp2[100] = 100;
			testParams.push(tmp2);
			var tmp3: Array = new Array();
			tmp3.push(1);
			tmp3["one"] = 1;
			testParams.push(tmp3);
			var tmp4: Object = {a: "foo", b: "bar"};
			testParams.push(tmp4);
			var tmp5: Array = new Array();
			tmp5.push(tmp4);
			tmp5.push(tmp4);
			testParams.push(tmp5);
			var now: Date = new Date();
			testParams.push(now);
			var tmp6: Array = new Array();
			tmp6.push(now);
			tmp6.push(now);
			testParams.push(tmp6);
			var tmp7: EchoClass = new EchoClass();
			tmp7.attr1 = "one";
			tmp7.attr2 = 1;
			testParams.push(tmp7);
			var tmp8: Array = new Array();
			tmp8.push(tmp7);
			tmp8.push(tmp7);
			testParams.push(tmp8);
			var remote: RemoteClass = new RemoteClass();
			remote.attribute1 = "one";
			remote.attribute2 = 2;
			testParams.push(remote);
			var tmp9: Array = new Array();
			var remote1: RemoteClass = new RemoteClass();
			remote1.attribute1 = "one";
			remote1.attribute2 = 1;
			tmp8.push(remote1);
			var remote2: RemoteClass = new RemoteClass();
			remote2.attribute1 = "two";
			remote2.attribute2 = 2;
			tmp9.push(remote2);
			testParams.push(tmp9);

			AMF0Count = testParams.length;
			
			// Add AMF3 specific tests below
			var ext: ExternalizableClass = new ExternalizableClass();
			testParams.push(ext);
			var tmp_1: Array = new Array();
			tmp_1.push(ext);
			tmp_1.push(ext);
			testParams.push(tmp_1);
			
			nc = new NetConnection();
			nc.addEventListener(NetStatusEvent.NET_STATUS, netStatusHandler); 
			onConnect(ObjectEncoding.AMF3);
        }
		
		private function onConnect(proto: uint): void {
			if (nc.connected) {
				nc.close();
			}
			nc.objectEncoding = proto;
			nc.connect("rtmp://localhost/echo");
			textArea.text = "Connecting using AMF" + proto + "...\n";
		}
		
		private function netStatusHandler(event: NetStatusEvent): void {
			textArea.text += event.info.code + ": " + event.info.description + "\n";
			switch(event.info.code) {
				case "NetConnection.Connect.Success":
					btnTest.enabled = true;
					btnDisconnect.enabled = true;
					onTest();
				break;
				case "NetConnection.Connect.Failed":
				case "NetConnection.Connect.Closed":
					onDisconnect();
				break;
			}
		}
		
		private function doTest(): void {
			if (testParams[testIndex] is String && (testParams[testIndex] as String).length >= 100)
				textArea.text += "Testing String with " + testParams[testIndex].length + " chars: ";
			else
				textArea.text += "Testing " + testParams[testIndex] + ": ";
			nc.call("echo", new Responder(this.onResult), testParams[testIndex]);
		}
		
		private function onTest(): void {
			textArea.text = "";
			testIndex = 0;
			testsFailed = 0;
			doTest();
		}

		private function onDisconnect(): void {
			btnConnect0.enabled = true;
			btnConnect3.enabled = true;
			btnTest.enabled = false;
			btnDisconnect.enabled = false;
		}

		private function extendedEqual(a: Object, b: Object): Boolean {
			var key: String;
			if (a == null && b != null) {
				return false;
			} else if (a != null && b == null) {
				return false;
			} else if (a is Array && b is Array) {
				if (a.length != (b as Array).length) {
					return false;
				}
				var i: Number;
				for (i=0; i<(a as Array).length; i++) {
					if (!extendedEqual((a as Array)[i], (b as Array)[i])) {
						return false;
					}
				}
				return true;
			} else if (a is ExternalizableClass && b is ExternalizableClass) {
				return (a.failed == b.failed && a.failed == 0);
			} else if (a is Object && !(b is Object)) {
				for (key in a) {
					if (!extendedEqual(a[key], (b as Array)[key])) {
						return false;
					}
				}
				return true;
			} else if (!(a is Object) && b is Object) {
				for (key in b) {
					if (!extendedEqual((a as Array)[key], b[key])) {
						return false;
					}
				}
				return true;
			} else if (a is Object && b is Object) {
				for (key in a) {
					if (!extendedEqual(a[key], b[key])) {
						return false;
					}
				}
				return true;
			} else {
				return (a == b);
			}
		}
		
		private function onResult(result: Object): void {
		    if (extendedEqual(testParams[testIndex], result)) {
				if (result == null)
					textArea.text += "OK (null)\n";
				else if (result is String && (result as String).length >= 1000)
					textArea.text += "OK (String with " + result.length + " chars)\n";
				else
					textArea.text += "OK (" + result.toString() + ")\n";
			} else {
				if (result == null)
					textArea.text += "FAILED (null)\n";
				else if (result is String && (result as String).length >= 1000)
					textArea.text += "FAILED (String with " + result.length + " chars)\n";
				else
					textArea.text += "FAILED (" + result.toString() + ")\n";
				testsFailed++;
			}
			testIndex += 1;
			var testCount: Number = testParams.length;
			if (nc.objectEncoding == ObjectEncoding.AMF0)
				testCount = AMF0Count;
			if (testIndex < testCount) {
				doTest();
			} else if (testsFailed == 0) {
				textArea.text += "Successfully ran " + testCount + " tests\n";
			} else {
				textArea.text += "Ran " + testCount + " tests, " + testsFailed + " failed\n";
			}
		}

    ]]>
    </mx:Script>
	
	<mx:HBox>
		<mx:Button id="btnConnect0" label="Connect AMF0" labelPlacement="left" click="onConnect(ObjectEncoding.AMF0)" />
		<mx:Button id="btnConnect3" label="Connect AMF3" labelPlacement="left" click="onConnect(ObjectEncoding.AMF3)" />
		<mx:Button id="btnTest" label="Test again" labelPlacement="left" click="onTest()" enabled="false" />
		<mx:Button id="btnDisconnect" label="Disconnect" labelPlacement="left" click="onDisconnect()" enabled="false" />
	</mx:HBox>
	<mx:TextArea id="textArea" width="100%" height="100%">
    </mx:TextArea>
</mx:Application>